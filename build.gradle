// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    // Define versions in a single place
    ext.versions = [
            // Sdk and tools
            'compileSdk'       : 28,
            'minSdk'           : 21,
            'targetSdk'        : 28,
            // App dependencies
            'supportLibrary'   : '28.0.0',
            'retrofit'         : '2.4.0',
            'butterknife'      : '8.8.1'
    ]
    ext.names = [
            'applicationId'    : 'com.mohamedabulgasem.mozulu'
    ]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'
    }
}

ext {
    // query git for the SHA, Tag and commit count. Use these to automate basic versioning.
    gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
    gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
